options  {
  STATIC=false;
  IGNORE_CASE=true;
}  
PARSER_BEGIN(ConstructorAST)
package implementacion.c_ast_descendente;

import implementacion.asint.TinyASint.*;
import implementacion.semops.SemOps;


public class ConstructorAST {
   private SemOps sem = new SemOps();
}
PARSER_END(ConstructorAST)

  TOKEN:{<#letra:["A"-"Z","a"-"z"]>}
  TOKEN:{<#dpos:["1"-"9"]>}
  TOKEN:{<#dig:<dpos>|"0">}
  TOKEN:{<#pdec:"." ((<dig>)*<dpos>|"0")>}
  TOKEN:{<#pexp:["e","E"] <Litent>>}
  TOKEN:{<#signo:["+","-"]>}
  SKIP:{<["\t"," ","\r","\b","\n"]>}  
  SKIP:{<"@"(~["\n"])*>}  
  TOKEN:{<Int: "int">}
  TOKEN:{<Real: "real">}
  TOKEN:{<Bool: "bool">}
  TOKEN:{<String: "string">}
  TOKEN:{<And: "and">}
  TOKEN:{<Or: "or">}
  TOKEN:{<Not: "not">}
  TOKEN:{<Null: "null">}
  TOKEN:{<True: "true">}
  TOKEN:{<False: "false">}
  TOKEN:{<Proc: "proc">}
  TOKEN:{<If: "if">}
  TOKEN:{<Then: "then">}
  TOKEN:{<Else: "else">}
  TOKEN:{<While: "while">}
  TOKEN:{<Do: "do">}
  TOKEN:{<Seq: "seq">}
  TOKEN:{<Begin: "begin">}
  TOKEN:{<End: "end">}
  TOKEN:{<Record: "record">}
  TOKEN:{<Array: "array">}
  TOKEN:{<Of: "of">}
  TOKEN:{<New: "new">}
  TOKEN:{<Delete: "delete">}
  TOKEN:{<Read: "read">}
  TOKEN:{<Write: "write">}
  TOKEN:{<Nl: "nl">}
  TOKEN:{<Var: "var">}
  TOKEN:{<Type: "type">}
  TOKEN:{<Id:<letra>(<letra>|<dig>|"_")*>}
  TOKEN:{<Litent:(<signo>)?((<dpos>(<dig>)*)|"0")>}
  TOKEN:{<Litreal:<Litent>(<pdec>|<pexp>|<pdec><pexp>)>}
  TOKEN:{<Litstr:"'" (~["\b", "\r", "\n", "'"])* "'">}
  
  
  Prog Init()    : {Prog prog;} {prog=Prog() <EOF> {return prog;}}
  
  Prog Prog()    : {Decs decs; Is insts; } 
                    {decs = Decs() insts = Insts() "." {return sem.prog(decs,insts);}}
  Decs Decs()    : {Decs decs;} 
  					{decs = LDecs()	{return decs;} | {return sem.decs_vacia();}}
  					
  Decs LDecs()	 : {Dec dec; Decs rldecs;}
  					{dec = Dec() rldecs = RLDecs(sem.decs_una(dec)) {return rldecs;}}
  					
  Decs RLDecs(Decs rldecs0h) : {Dec dec; Decs rldecs1;}
  					{dec = Dec() rldecs1 = RLDecs(sem.decs_muchas(rldecs0h, dec)) {return rldecs1;} |
  					{return rldecs0h;}} 
  					
  Dec Dec()		 : {Token id; Tipo tipo; PForms pforms; Decs decs; Is insts;}
  					{<Var> id=<Id> ":" tipo=Tipo() ";" {return sem.decVar(tipo, new StringLocalizado(id));} |
  					<Type> id=<Id> ":" tipo=Tipo() ";"{return sem.decTipo(tipo, new StringLocalizado(id));} |
  					<Proc> id=<Id> pforms=PForms() decs=Decs() insts=Insts() ";" {return sem.decProc(new StringLocalizado(id),pforms,decs,insts);}}
  					
  Tipo TipoBase() : {Token id;}
  					{<Int> {return sem.tipoInt();} |
  					<Real> {return sem.tipoReal();} |
  					<String> {return sem.tipoString();} |
  					<Bool> {return sem.tipoBool();} |
  					id=<Id> {return sem.tipoRef(new StringLocalizado(id));}}
  					
  Tipo Tipo()	: {Tipo tipo; Token id; Campos campos;}
  					{tipo=TipoBase() {return tipo;} |
  					<Array> "[" id=<Litent> "]" <Of> tipo=TipoBase() {return sem.tipoArray(new StringLocalizado(id), tipo);} |
  					<Record> campos=Campos() <End> {return sem.tipoRecord(campos);} |
  					"^" tipo=TipoBase() {return sem.tipoPointer(tipo);}}
  					
  					
 PForms PForms() : {PForms pforms;}
 					{"(" pforms=LPFormsPar() ")" {return pforms;}}
 	
 PForms LPFormsPar() : {PForms pforms;}
 					{pforms=LPForms() {return pforms;} | {return sem.pForm_vacia();}}
 					
 PForms LPForms() : {PForm pform; PForms pforms;}
 					{pform=PForm() pforms=RLPForms(sem.pForm_una(pform)) {return pforms;}}
 					
 					
 PForms RLPForms(PForms pforms0h) : {PForm pform; PForms pforms1;}
 					{"," pform=PForm() pforms1=RLPForms(sem.pForm_muchas(pforms0h, pform)) {return pforms1;} |
 					{return pforms0h;}}
 					
 PForm PForm() : {Token id; Tipo tipo;}
 					{<Var> id=<Id> ":" tipo=Tipo() {return sem.pFormRef(new StringLocalizado(id), tipo);} |
 					id=<Id> ":" tipo=Tipo() {return sem.pForm(new StringLocalizado(id), tipo);}}
 					
 PReals PReals() : {PReals preals;}
 					{"(" preals=LPRealsPar() ")" {return preals;}}
 	
 PReals LPRealsPar() : {PReals preals;}
 					{preals=LPReals() {return preals;} | {return sem.pReal_ninguno();}}
 					
 PReals LPReals() : {PReal preal; PReals preals;}
 					{preal=PReal() preals=RLPReals(sem.pReal_uno(preal)) {return preals;}}
 					
 					
 PReals RLPReals(PReals preals0h) : {PReal preal; PReals preals1;}
 					{"," preal=PReal() preals1=RLPReals(sem.pReal_muchos(preals0h, preal)) {return preals1;} |
 					{return preals0h;}}
 					
 PReal PReal() : {Exp exp;}
 					{exp = Exp() {return sem.pReal(exp);}}
 					
 					
 Campos Campos() : {Campo campo; Campos campos;}
 					{campo=Campo() campos=RCampos(sem.campos_uno(campo)) {return campos;}}
 					
 Campos RCampos(Campos campos0) : {Campo campo; Campos campos1;}
 					{campo=Campo() campos1=RCampos(sem.campos_muchos(campos0, campo)) {return campos1;} |
 					{return campos0;}}
 					
 Campo Campo() : {Token id; Tipo tipo;}
 					{id=<Id> ":" tipo=Tipo() ";" {return sem.campo(tipo, new StringLocalizado(id));}}
 					
 					
 					
Is Insts() : {Is insts;}
					{"begin" insts=LInstsAux() "end" {return insts;}}
					
Is LInstsAux() : {Is insts;}
					{insts=LInsts() {return insts;} | {return sem.is_vacia();}}

Is LInsts() 	: 	{I inst; Is insts;}
					{inst=Inst() insts=RLInsts(sem.is_una(inst)) {return insts;}}
					
Is RLInsts(Is insts0) : {I inst; Is insts1;}
					{inst=Inst() insts1=RLInsts(sem.is_muchas(insts0, inst)) {return insts1;} |
					{return insts0;}}
					
void PuntoComaONada() : {} {";" {return;} | {return;}}
					
I Inst() : {Exp e0, e1; Is insts0, insts1;I rinst, rinst2; Decs decs; PReals preals;}
				{e0=Exp() rinst2=RInst2(e0) {return rinst2;} |
				<If> e0=Exp() <Then> insts0=LInsts() rinst=RInst(e0,insts0) {return rinst;} |
				<While> e0=Exp() <Do> insts0=LInsts() <End> PuntoComaONada() {return sem.while1(e0, insts0);} |
				<Read> e0 = Exp() ";" {return sem.read(e0);} |
				<Write> e0 = Exp() ";" {return sem.write(e0);} |
				<Nl> ";" {return sem.nl();} |
				<New> e0 = Exp() ";" {return sem.new1(e0);} |
				<Delete> e0 = Exp() ";" {return sem.delete(e0);} |
				<Seq> decs=Decs() insts0=Insts() PuntoComaONada() {return sem.iComp(decs,insts0);}}
				
I RInst(Exp ah1, Is ah2) : {Is is;}
				{<End> PuntoComaONada() {return sem.if_then(ah1, ah2);} | 
				<Else> is = LInsts() <End> PuntoComaONada() {return sem.if_then_else(ah1, ah2, is);}}
				
I RInst2(Exp ah) : {Is is;Exp e1; PReals preals;}
				{"=" e1=Exp() ";" {return sem.asig(ah,e1);} | 
				preals=PReals() ";" {return sem.call(ah ,preals);}}
				
Exp Exp() : {Exp e0;}
			{e0=Exp0() {return e0;}}
			
Exp Exp0() : {Exp e0, re1;}
			{e0=Exp1() re1=RExp0(e0) {return re1;}}
			
Exp RExp0(Exp rexp0h) : {String op; Exp e1;}
			{op=Op0() e1=Exp1() {return sem.expb(op, rexp0h, e1);} | {return rexp0h;}}
			
String Op0() : {}
			{"<" {return "<";} |
			">" {return ">";} |
			"<=" {return "<=";} |
			">=" {return ">=";} |
			"==" {return "==";} |
			"!=" {return "!=";}}
			
Exp Exp1() : {Exp e2, rexp1, rexp1p;}
			{e2 = Exp2() rexp1=RExp1(e2) rexp1p=RExp1P(rexp1) {return rexp1p;}}
			
Exp RExp1(Exp rexp10) : {Exp e2;}
			{"+" e2=Exp2() {return sem.expb("+", rexp10, e2);} |
			{return rexp10;}}
			
Exp RExp1P(Exp rexp1p0) : {Exp e2, rexp1p1;}
			{"-" e2=Exp2() rexp1p1=RExp1P(sem.expb("-", rexp1p0, e2)) {return rexp1p1;} |
			{return rexp1p0;}}
			
Exp Exp2() : {Exp e3, rexp2;}
			{e3=Exp3() rexp2=RExp2(e3) {return rexp2;}}
			
			
Exp RExp2(Exp rexp20) : {Exp e2, e3;}
			{"and" e3=Exp3() {return sem.expb("and", rexp20, e3);} |
			"or" e2=Exp2() {return sem.expb("or", rexp20, e2);} |
			{return rexp20;}}
			
			
Exp Exp3() : {Exp e4, rexp3;}
			{e4=Exp4() rexp3=RExp3(e4) {return rexp3;}}
	
	
Exp RExp3(Exp rexp30) : {String op; Exp e4, rexp31;}
			{op=Op3() e4=Exp4() rexp31=RExp3(sem.expb(op, rexp30, e4)) {return rexp31;} |
			{return rexp30;}}
			
String Op3() : {}
			{"/" {return "/";} |
			"*" {return "*";} |
			"%" {return "%";}}
			
			
Exp Exp4() : {String op; Exp e4, e5;}
			{op=Op4() e4=Exp4()	{return sem.expu(op, e4);} |
			e5=Exp5() {return e5;}}
						
String Op4() : {}
			{"-" {return "-";} |
			"not" {return "not";}}
			
			
Exp Exp5() : {Exp e6, rexp5;}
			{e6=Exp6() rexp5=RExp5(e6) {return rexp5;}}
			
Exp RExp5(Exp rexp50) : {Token id; Exp exp0, rexp51;}
			{"[" exp0=Exp0() "]" rexp51=RExp5(sem.indx(rexp50, exp0)) {return rexp51;} |
			"^" rexp51=RExp5(sem.dref(rexp50)) {return rexp51;} |
			"." id=<Id> rexp51=RExp5(sem.acc(rexp50, new StringLocalizado(id))) {return rexp51;} |
			{return rexp50;}}
			
			
Exp Exp6() : {Token id; Exp e;}
			{id=<Litent> {return sem.litInt(new StringLocalizado(id));} |
			id=<Litreal> {return sem.litReal(new StringLocalizado(id));} |
			id=<Litstr> {return sem.litStr(new StringLocalizado(id));} |
			id=<True> {return sem.true1(new StringLocalizado(id));} |
			id=<False> {return sem.false1(new StringLocalizado(id));} |
			id=<Id> {return sem.id(new StringLocalizado(id));} |
			id=<Null> {return sem.null1(new StringLocalizado(id));} |
			"(" e=Exp0() ")" {return e;}}

 